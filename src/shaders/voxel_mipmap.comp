#version 430

##import voxel_shared

layout (local_size_x = 16, local_size_y = 16) in;

layout (binding = 0, r32ui) readonly uniform uimage2D previousMipList;
layout (binding = 1, r32ui) uniform uimage2D currentMipList;

layout (binding = 2, rgba8) readonly uniform image3D previousVoxelAlpha;
layout (binding = 3, rgba16) readonly uniform image3D previousVoxelRadiance;
layout (binding = 4, rgba8) uniform image3D currentVoxelAlpha;
layout (binding = 5, rgba16) uniform image3D currentVoxelRadiance;

layout (binding = 0) uniform atomic_uint previousFragListSize;
layout (binding = 0, offset = 16) uniform atomic_uint nextFragListSize;
layout (binding = 0, offset = 20) uniform atomic_uint nextComputeSize;

uniform int mipLevel = 1;

void main() {
	uint index = gl_WorkGroupID.x * (gl_WorkGroupSize.x * gl_WorkGroupSize.y) + gl_LocalInvocationIndex;
	uint size = atomicCounter(previousFragListSize);
	if (index >= size) return;

	uint packedPosition = imageLoad(previousMipList, ivec2(index & MaxFragListMask[mipLevel - 1], index >> FragListWidthBits[mipLevel - 1])).r;
	ivec3 position = ivec3(
		(packedPosition >> 20) & 0x3FF,
		(packedPosition >> 10) & 0x3FF,
		packedPosition & 0x3FF
	);
	ivec3 mipPosition = position >> 1;
	ivec3 floorPosition = mipPosition << 1;

	// Alpha on each axis, front and back
	vec3 alpha = vec3(0);
	vec3 alphaBack = vec3(0);
	float count = 0.0;
	vec3 radiance = vec3(0);

	for (int x = 0; x < 2; x++) {
		for (int y = 0; y < 2; y++) {
			for (int z = 0; z < 2; z++) {
				// This invocation should only run if we're the first value in the group.
				if (position == floorPosition + ivec3(x, y, z) && count > 0) return;

				vec4 prevAlpha = imageLoad(previousVoxelAlpha, floorPosition + ivec3(x, y, z));
				alpha += vec3(x, y, z) * prevAlpha.xyz;
				alphaBack += (1 - vec3(x, y, z)) * prevAlpha.xyz;

				radiance += imageLoad(previousVoxelRadiance, floorPosition + ivec3(x, y, z)).rgb;
				count += 1 - step(0, -prevAlpha.a);
			}
		}
	}

	alpha *= 0.5;
	alpha += alphaBack * 0.5 * (1.0 - alpha);
	imageStore(currentVoxelAlpha, mipPosition, vec4(alpha, 1.0));
	imageStore(currentVoxelRadiance, mipPosition, vec4(radiance / count, 1.0));

	uint nextIndex = atomicCounterIncrement(nextFragListSize);
	if (nextIndex % MipmapWorkGroupSize == 0) atomicCounterIncrement(nextComputeSize);

	uint packedData = (uint(mipPosition.x) & 0x3FF) << 20;
	packedData += (uint(mipPosition.y) & 0x3FF) << 10;
	packedData += uint(mipPosition.z) & 0x3FF;
	imageStore(currentMipList, ivec2(nextIndex & MaxFragListMask[mipLevel], nextIndex >> FragListWidthBits[mipLevel]), uvec4(packedData));
}
